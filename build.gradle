plugins {
    id 'java'
    id 'uk.gov.hmcts.java' version '0.12.63'
    id 'io.gatling.gradle' version '3.11.5.2'
}

group = 'uk.gov.hmcts'
version = '0.0.1'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencyCheck {
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencies {
    gatling group: 'org.postgresql', name: 'postgresql', version: '42.7.4'
    gatling 'com.github.hmcts:juror-generation-support-library:1.5.4'
}

// Default gatlingRun configuration
gatlingRun {
    jvmArgs = ['-Xms4G', '-Xmx8G']
    simulationClassName = System.getenv("SimulationBlock")
    doFirst {
        println "Running simulation: " + simulationClassName
    }
}

// Run Command = & .\gradlew.bat runCaseRetentionForChildObjectsSimulation

// Function to register a task for a simulation
def registerSimulationTask(taskName, simulationClassName) {
    tasks.register(taskName) {
        doLast {
            println "Running simulation: ${simulationClassName}"
        }
        // Set the simulation class and make this task depend on gatlingRun
        finalizedBy(gatlingRun)
        gatlingRun.simulationClassName = simulationClassName
    }
}
// List of simulations with their task names and simulation class names
def simulations = [

    [task: 'runLoginSimulation', class: 'simulations.Scripts.PerformanceTests.LoginSimulation'],
    [task: 'runCreateAccountSimulation', class: 'simulations.Scripts.PerformanceTests.CreateAccountSimulation'],
    [task: 'runApproveAccountSimulation', class: 'simulations.Scripts.PerformanceTests.ApproveAccountSimulation'],
    [task: 'runUserExistsSimulation', class: 'simulations.Scripts.PerformanceTests.UserExistsSimulation'],
    [task: 'runDeleteAccountSimulation', class: 'simulations.Scripts.PerformanceTests.DeleteAccountSimulation'],
    [task: 'runMAC_01aSimulation', class: 'simulations.Scripts.PerformanceTests.MAC_01aSimulation']        

]

// Register tasks dynamically
simulations.each { simulation ->
    registerSimulationTask(simulation.task, simulation.class)
}


checkstyleGatling {
    maxWarnings = 3035
}

tasks.register('runAllStyleChecks') {
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
    dependsOn 'checkstyleGatling'
}
